
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура НачалоВыполнения(Задача) Экспорт 
	МенеджерЗаписи = РегистрыСведений.РЗ_Хронометраж.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Задача = Задача;
	МенеджерЗаписи.Прочитать();
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Задача = Задача;
		МенеджерЗаписи.ДатаНачала = ТекущаяДатаСеанса();
		МенеджерЗаписи.Записать();
	Иначе
		//Выполнение уже запущено
	КонецЕсли;
КонецПроцедуры

Процедура ЗавершениеВыполнения(Задача) Экспорт 

	//Закончить таймер и создать документ
	МенеджерЗаписи = РегистрыСведений.РЗ_Хронометраж.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Задача = Задача;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		ВремяВыполнения = ТекущаяДатаСеанса() - МенеджерЗаписи.ДатаНачала;
		Часы = Окр(ВремяВыполнения / 3600, 2);
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
		ДанныеЗаполнения.Вставить("Видоперации", Перечисления.РЗ_ВидыУчетаРабочегоВремени.Программирование);
		ДанныеЗаполнения.Вставить("Комментарий", "По данным хронометража");
		ТаблицаЗначений = Новый ТаблицаЗначений;
		ТаблицаЗначений.Колонки.Добавить("Контакт");
		ТаблицаЗначений.Колонки.Добавить("Задача");
		ТаблицаЗначений.Колонки.Добавить("КоличествоЧасов");
		НоваяСтрока = ТаблицаЗначений.Добавить();
		НоваяСтрока.Контакт = Задача.Заказчик;
		НоваяСтрока.Задача = Задача;
		НоваяСтрока.КоличествоЧасов = Часы;
		ДанныеЗаполнения.Вставить("УчетВремени", ТаблицаЗначений);
		ДанныеЗаполнения.Вставить("Хронометраж", Истина);
		
		Документ = Документы.РЗ_УчетРабочегоВремениНовый.СоздатьДокумент();
		Документ.Заполнить(ДанныеЗаполнения);
		Документ.Записать(РежимЗаписиДокумента.Проведение);
		
		МенеджерЗаписи.Удалить();
	Иначе
		//Выполнение задачи не было запущено
	КонецЕсли;

КонецПроцедуры

#КонецЕсли
